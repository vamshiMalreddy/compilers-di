/* Simple +/-/* expression language; parser evaluates constant expressions on the fly*/
/**
 *  Package and Import Specifications
 */
import java_cup.runtime.*;
import java.io.*;

/**
 *  Usercode Components
 */
parser code {:
    // Connect this parser to a scanner!
    Scanner s;
    Parser(Scanner s){ this.s=s; }
:}

/* define how to connect to the scanner! */
scan with {: return s.next_token(); :};

/**
 *  Symbol Lists
 */

/* Terminals (tokens returned by the scanner). */
terminal    IDENTIFIER, STRING_LITERAL;
terminal    LPAREN, RPAREN, LCURLYBRACKET, RCURLYBRACKET;
terminal    PLUS, EQUALS, IN, COMMA, IF, ELSE;

/*  Non terminals */
non terminal    program, func_declarations, func_declare;
non terminal    func_name, func_param_list, func_block;
non terminal    parameter, func_calls, func_call;
non terminal    func_arguments, func_argument;
non terminal    func_param_lst_inner, func_call_inner, fun_params_inner, parameter_inner;
non terminal    func_body, statements, statement, expression;
/**
 *  Precedence Declarations
 */
precedence left PLUS;
precedence left EQUALS;
precedence left IN;

/**
 *  The Grammar Rules
 */
//TODO add exceptions for the file
program                  ::= func_declarations:fd                               {: File file = new File("out/Main.java");
                                                                                   if(!file.exists()){file.createNewFile();}
                                                                                   FileWriter fw = new FileWriter(file,true);
                                                                                   BufferedWriter bw = new BufferedWriter(fw);
                                                                                   PrintWriter pw = new PrintWriter(bw);
                                                                                   pw.println("}");
                                                                                   pw.close(); :}
;

func_declarations       ::= func_declare:decl func_declarations:fd              {: File file = new File("out/Main.java");
                                                                                   if(!file.exists()){file.createNewFile();}
                                                                                   FileWriter fw = new FileWriter(file,true);
                                                                                   BufferedWriter bw = new BufferedWriter(fw);
                                                                                   PrintWriter pw = new PrintWriter(bw);
                                                                                   pw.println(decl);
                                                                                   pw.close(); :}
                        | func_calls:fc                                         {: File file = new File("out/Main.java");
                                                                                   if(!file.exists()){file.createNewFile();}
                                                                                   FileWriter fw = new FileWriter(file,false);
                                                                                   BufferedWriter bw = new BufferedWriter(fw);
                                                                                   PrintWriter pw = new PrintWriter(bw); pw.println("public class Main {\n\tpublic static void main(String[] args) {\n" + fc + "\t}");
                                                                                   pw.close(); :}
;

func_calls              ::= func_call:call func_calls:fc                        {: RESULT = call + "\n" + fc ;:}
                        | /* Empty */                                           {: RESULT = "";:}
;

func_declare            ::= IDENTIFIER:func_id LPAREN func_param_list:parlist RPAREN LCURLYBRACKET func_body:body RCURLYBRACKET
                                                                                {: RESULT = "\n\tpublic static String " + func_id + " (" + parlist + ") {\n" + body + "\n\t}"; :}
;

func_call               ::= IDENTIFIER:func_id LPAREN func_param_list:parlist RPAREN
                                                                                {: RESULT = "\t\tSystem.out.println(" + func_id + "(" + parlist + "));"; :}
;


func_param_list         ::= parameter:par                                       {: RESULT = par; :}
                        | func_param_list:parlist COMMA parameter:par           {: RESULT = parlist + ", " + par; :}
                        | /* Empty */                                           {: RESULT = ""; :}
;

func_call_inner         ::= IDENTIFIER:func_id LPAREN func_param_lst_inner:parlist RPAREN
                                                                                {: RESULT = func_id + "(" + parlist + ")"; :}
;

func_param_lst_inner    ::= parameter_inner:par                                 {: RESULT = par; :}
                        | func_param_lst_inner:parlist COMMA parameter_inner:par{: RESULT = parlist + ", " + par; :}
                        | /* Empty */                                           {: RESULT = ""; :}
;

parameter_inner         ::= IDENTIFIER:param                                    {: RESULT = param; :}
                        | STRING_LITERAL:str                                    {: RESULT = str; :}
                        | func_call_inner:fc                                    {: RESULT = fc; :}
;

//TODO se top level call mono identifier!!
parameter               ::= IDENTIFIER:param                                    {: RESULT = "String " + param; :}
                        | STRING_LITERAL:str                                    {: RESULT = str; :}
                        | func_call_inner:fc                                    {: RESULT = fc; :}
;

func_body               ::= statements:stmts                                    {: RESULT = "\t\t" + stmts; :}
                        | /* Empty */                                           {: RESULT = ""; :}
;

statements              ::= IF LPAREN expression:expr RPAREN statements:stmt1 ELSE statements:stmt2
                                                                                {: RESULT = "if (" + expr + ") { " + stmt1 + "}" + " else { " + stmt2 + " }";:}
                        | expression:expr                                       {: RESULT = "return " + expr + ";"; :}
;

expression              ::= IDENTIFIER:idntf                                    {: RESULT = idntf; :}
                        | STRING_LITERAL:str                                    {: RESULT = str; :}
                        | func_call_inner:fc                                {: RESULT = fc; :}
                        | expression:expr1 EQUALS expression:expr2              {: RESULT = expr1 + ".equals("+expr2+")"; :}
                        | expression:expr1 IN expression:expr2                  {: RESULT = expr2 + ".contains("+expr1+")"; :}
                        | expression:expr1 PLUS expression:expr2                {: RESULT = expr1 +" + "+ expr2; :}
;
